# Author: Lucas Vilas-Boas
# Year: 2025
# Repo: https://github.com/lucoiso/luvk

INCLUDE(CheckIPOSupported)

MACRO(_GET_PERFORMANCE_FLAGS _OUT_COMPILE _OUT_LINK)
    IF(MSVC)
        SET(${_OUT_COMPILE} "/O2 /Oi /Ot /GT /GL /Gw /arch:AVX2 /MP")
        SET(${_OUT_LINK} "/LTCG /OPT:REF /OPT:ICF")
    ELSE()
        SET(${_OUT_COMPILE} "-O3 -march=native -ffast-math -fno-plt -fomit-frame-pointer")
        SET(${_OUT_LINK} "")
        IF(CMAKE_CXX_COMPILER_ID STREQUAL "Clang" AND NOT APPLE)
            SET(${_OUT_LINK} "-fuse-ld=lld")
        ENDIF()
    ENDIF()
ENDMACRO()

FUNCTION(ENABLE_GLOBAL_RELEASE_PERFORMANCE)
    CHECK_IPO_SUPPORTED(RESULT IPO_SUPPORTED)
    IF(IPO_SUPPORTED)
        SET(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELEASE TRUE PARENT_SCOPE)
        SET(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELWITHDEBINFO TRUE PARENT_SCOPE)
    ENDIF()

    SET(CMAKE_CXX_VISIBILITY_PRESET HIDDEN PARENT_SCOPE)
    SET(CMAKE_VISIBILITY_INLINES_HIDDEN ON PARENT_SCOPE)

    _GET_PERFORMANCE_FLAGS(C_FLAGS L_FLAGS)

    SET(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} ${C_FLAGS}" PARENT_SCOPE)
    SET(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} ${C_FLAGS}" PARENT_SCOPE)
    SET(CMAKE_EXE_LINKER_FLAGS_RELEASE "${CMAKE_EXE_LINKER_FLAGS_RELEASE} ${L_FLAGS}" PARENT_SCOPE)
    SET(CMAKE_SHARED_LINKER_FLAGS_RELEASE "${CMAKE_SHARED_LINKER_FLAGS_RELEASE} ${L_FLAGS}" PARENT_SCOPE)

    ADD_DEFINITIONS(-DNDEBUG)
ENDFUNCTION()

FUNCTION(ENABLE_TARGET_RELEASE_PERFORMANCE TARGET_NAME)
    SET_TARGET_PROPERTIES(${TARGET_NAME} PROPERTIES UNITY_BUILD ON)

    CHECK_IPO_SUPPORTED(RESULT IPO_SUPPORTED)
    IF(IPO_SUPPORTED)
        SET_TARGET_PROPERTIES(${TARGET_NAME} PROPERTIES
                              INTERPROCEDURAL_OPTIMIZATION_RELEASE TRUE
        )
    ENDIF()

    _GET_PERFORMANCE_FLAGS(C_FLAGS L_FLAGS)

    SEPARATE_ARGUMENTS(C_FLAGS_LIST NATIVE_COMMAND "${C_FLAGS}")
    SEPARATE_ARGUMENTS(L_FLAGS_LIST NATIVE_COMMAND "${L_FLAGS}")

    TARGET_COMPILE_OPTIONS(${TARGET_NAME} PRIVATE $<$<CONFIG:RELEASE>:${C_FLAGS_LIST}>)
    TARGET_LINK_OPTIONS(${TARGET_NAME} PRIVATE $<$<CONFIG:RELEASE>:${L_FLAGS_LIST}>)
ENDFUNCTION()